float3 FresnelSchlick(float3 fresnelReflectance, float cosTheta)
{
    return fresnelReflectance + (1.0 - fresnelReflectance) * pow(saturate(1 - cosTheta), 5);
}

float VisSchlick(float roughness, float cosLo, float cosLi)
{
    float r = roughness + 1;
    float k = (r * r) / 8;
    float schlickV = cosLo * (1 - k) + k;
    float schlickL = cosLi * (1 - k) + k;
    return 1.0 / (schlickV * schlickL);
}

float TrowbridgeReitzNDF(float NdotH, float roughness)
{
    float roughnessSqr = roughness*roughness;
    float Distribution = NdotH*NdotH * (roughnessSqr-1.0) + 1.0;
    return roughnessSqr / (3.1415926535 * Distribution*Distribution);
}

float2 EnvBRDFApprox(float VdotN, float roughness)
{
    float4 c0 = float4(-1, -0.0275, -0.572, 0.022);
    float4 c1 = float4(1, 0.0425, 1.04, -0.04);
    float4 r = roughness * c0 + c1;
    float a004 = min(r.x * r.x, exp2(-9.28 * VdotN)) * r.x + r.y;
    return float2(-1.04, 1.04) * a004 + r.zw;
}

float SchlickGGXGeometricShadowingFunction(float NdotL, float NdotV, float roughness)
{
    float k = roughness / 2;


    float SmithL = (NdotL)/ (NdotL * (1- k) + k);
    float SmithV = (NdotV)/ (NdotV * (1- k) + k);


	float Gs =  (SmithL * SmithV);
	return Gs;
}